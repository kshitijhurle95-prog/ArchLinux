import QtQuick
import "Singletons"
import "lib/hittest.js" as Hit

// The eight grips on the captured region. Centres are computed from the global
// selection and drawn only by the monitor that actually contains them, so a
// selection spanning two heads shows one grip per edge rather than a duplicate
// pair at the seam.
Item {
    id: grips

    required property var globalRect
    required property int sx
    required property int sy

    signal started(string role, real gx, real gy)
    signal moved(real gx, real gy)
    signal ended()

    readonly property int hit: 20
    readonly property int dot: 11

    readonly property var cursors: ({
        "tl": Qt.SizeFDiagCursor, "br": Qt.SizeFDiagCursor,
        "tr": Qt.SizeBDiagCursor, "bl": Qt.SizeBDiagCursor,
        "t": Qt.SizeVerCursor, "b": Qt.SizeVerCursor,
        "l": Qt.SizeHorCursor, "r": Qt.SizeHorCursor
    })

    Repeater {
        model: Hit.handleRoles()

        Item {
            id: grip
            required property var modelData
            readonly property string role: modelData
            readonly property var centre: grips.globalRect
                ? Hit.handleCenter(grips.globalRect, grip.role)
                : null
            readonly property real lx: centre ? centre.x - grips.sx : 0
            readonly property real ly: centre ? centre.y - grips.sy : 0

            visible: centre !== null
                && lx >= 0 && lx <= grips.width
                && ly >= 0 && ly <= grips.height

            x: lx - grips.hit / 2
            y: ly - grips.hit / 2
            width: grips.hit
            height: grips.hit

            Rectangle {
                anchors.centerIn: parent
                width: grips.dot
                height: grips.dot
                radius: 2
                color: ma.pressed ? Theme.accent : Theme.ink
                border.color: Theme.accent
                border.width: 1.5
                scale: ma.containsMouse ? 1.25 : 1.0
                Behavior on scale { NumberAnimation { duration: Theme.snap } }
            }

            MouseArea {
                id: ma
                anchors.fill: parent
                hoverEnabled: true
                cursorShape: grips.cursors[grip.role]
                onPressed: grips.started(grip.role, grip.lx + grips.sx, grip.ly + grips.sy)
                onPositionChanged: (m) => {
                    if (pressed) grips.moved(m.x + grip.x + grips.sx, m.y + grip.y + grips.sy);
                }
                onReleased: grips.ended()
            }
        }
    }
}
